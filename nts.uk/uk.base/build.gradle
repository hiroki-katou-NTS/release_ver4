import org.gradle.api.Project

apply plugin: 'java'

buildscript {
	ext {
		version = "1.0.0"
		localRepo = "${project(':base.web').projectDir}/../../uk.repo"
	}
	
	repositories {
		// use local repository
		flatDir {
			dirs localRepo
	 	}
	}
	
	dependencies {
		// compress js plugin
		classpath 'com.eriwen:gradle-js-plugin:1.12.0'

		classpath "com.bmuschko:gradle-cargo-plugin:2.2.1"
		
		classpath "de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.5"
	}
}

//すべてのプロジェクトに適応すべき内容はここに追記する
allprojects {
	apply from: "$rootDir/gradle/properties.gradle"
	apply from: "$rootDir/gradle/library_properties.gradle"

	//ビルド及び実行は、Java8を対象とする
	sourceCompatibility = "$javaVersion"
	targetCompatibility = "$javaVersion" 

	task cleanandbuildallproject (dependsOn: ['clean', 'build']) {
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'eclipse-wtp'
	
	repositories {
		flatDir {
			dirs localRepo
	 	}
	}
	
	//compileJavaとcompileTestJavaの実行時の文字コードをunicodeで指定
	[compileJava, compileTestJava]*.options*.encoding = "$defaultEncoding"
	
	dependencies {

	}
}

//=====================nts.uk.com.js.web↓============================
project (':base.web:nts.uk.com.js.web') {
	apply plugin: 'war'
	
	war {
		exclude '**/*.ts'
		exclude '**/*.js.map'
		baseName= "nts.uk.com.js.web"
		includeEmptyDirs false
	}
	
	task tsc(type: Exec) {
		commandLine 'cmd', '/c', 'tsc'
		args '--sourceMap', '--declaration'
	}
	
	task tscw(type: Exec) {
		commandLine 'cmd', '/c', 'tsc'
		args '--watch', '--sourceMap', '--declaration'
	}
	
	task csc(type: Exec) {
		workingDir 'src/main/webapp/lib/nittsu/ui/style'		
		commandLine 'cmd', '/c', 'node-sass'
		args 'sass/base.scss', 'stylesheets/base.css', '--output-style', 'compressed', '--source-comments', 'false', '--source-map', 'true'
	}
}